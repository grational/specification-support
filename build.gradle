/*
 * This build file was auto generated by 'gigawatt'
 * @author d7392
 * @date 02-08-2018 07.39
 */

// [plugins] this block must be the first
plugins {
	// support for groovy
	id 'groovy'
	// add support for api
	id 'java-library'
	// required by jitpack
	id 'maven-publish'
	// groovy code linter
	id 'codenarc'
	// test code coverage plugin
	id 'net.saliman.cobertura' version '4.0.0'
	// awesome git tag / gradle version integration
	id 'pl.allegro.tech.build.axion-release' version '1.13.5'
}

// [general] configuration
project.ext {
	groovyVersion   = '3.0.9'
	spockVersion    = '2.0-groovy-3.0'
	codenarcVersion = '2.2.0'
	slf4jVersion    = '1.7.32'
	wiremockVersion = '2.27.2'
}

scmVersion {
	tag {
		prefix = 'release-'
	}
}

project.with {
	//project.name is set in the settings.gradle with rootProject.name
	group                 'it.grational'
	version               scmVersion.version // (remove -SNAPSHOT for releases)
	description           "A 'gigawatt' powered project."
}
// [crosscompile] source and target java version
allprojects {
	sourceCompatibility = '1.8'
	targetCompatibility = sourceCompatibility
}

repositories {
	jcenter()

	// leverage github as a maven repository
	maven { url 'https://jitpack.io' }
}

dependencies {
	// Groovy language support
	implementation (
		"org.codehaus.groovy:groovy:$groovyVersion"
	)

	api (
		"com.github.tomakehurst:wiremock:$wiremockVersion"
	)

	// We use the awesome Spock testing and specification framework
	testImplementation (
		"org.spockframework:spock-core:$spockVersion"
	)
}

// codenarc groovy source analyses
codenarc{
	toolVersion = "$codenarcVersion"

	codenarcMain {
		configFile = rootProject.file('config/codenarc/ruleset.groovy')
		exclude "**/package-info.*"
		ignoreFailures true       // does not block builds
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
	codenarcTest {
		configFile = rootProject.file('config/codenarc/ruleset-test.groovy')
		ignoreFailures true
		maxPriority1Violations 0
		maxPriority2Violations 10
		maxPriority3Violations 20
	}
}

// [tasks] configuration //
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.grational'
			artifactId = 'specification-support'
			version = scmVersion.version // '0.5.0' (remove -SNAPSHOT for releases)

			from components.java
		}
	}
}

jar {
	// execute check task when create the jar
	dependsOn 'check'
}

check {
	// create the code coverage report
	dependsOn 'cobertura'
}

// [tasks] definition
tasks.withType(Test) {
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors() / 2
}
task uniTest(type: Test) {
	description = 'unit tests task'
	exclude '**/*ISpec*'
	exclude '**/*FSpec*'
}
task intTest(type: Test) {
	description = 'integration tests task'
	exclude '**/*USpec*'
	exclude '**/*FSpec*'
}
task funTest(type: Test) {
	description = 'functional tests task'
	exclude '**/*USpec*'
	exclude '**/*ISpec*'
}
gradle.startParameter.excludedTaskNames += "uniTest"
gradle.startParameter.excludedTaskNames += "intTest"
gradle.startParameter.excludedTaskNames += "funTest"
// vim: ft=groovy:fdm=indent
